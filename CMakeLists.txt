cmake_minimum_required(VERSION 3.3)

project(simple-platformer-build
	LANGUAGES NONE
)

include(ExternalProject)
ExternalProject_Add(Corrade
	SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/deps/corrade"
	LIST_SEPARATOR "#"
	CMAKE_ARGS
		"-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
		${CUSTOM_CORRADE_ARGS}
	TEST_EXCLUDE_FROM_MAIN 1
)
ExternalProject_Add(Magnum
	DEPENDS
		Corrade
	SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/deps/magnum"
	LIST_SEPARATOR "#"
	CMAKE_ARGS
		"-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
		"-DWITH_SDL2APPLICATION=ON"
		"-DWITH_SHADERS=ON"
		"-DWITH_TEXT=ON"
		${CUSTOM_MAGNUM_ARGS}
	TEST_EXCLUDE_FROM_MAIN 1
)
if(NOT MSVC)
	set(FREETYPE_TARGET "FreeType")
	ExternalProject_Add(${FREETYPE_TARGET}
		SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/deps/freetype2"
		LIST_SEPARATOR "#"
		CMAKE_ARGS
			"-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
			"-DBUILD_SHARED_LIBS=ON"
			${CUSTOM_FREETYPE_ARGS}
		TEST_EXCLUDE_FROM_MAIN 1
	)
else()
	message(WARNING "FreeType needs to be built as shared, not static - see http://stackoverflow.com/a/7387618/1959975 for help")
endif()
ExternalProject_Add(MagnumPlugins
	DEPENDS
		Corrade
		Magnum
		${FREETYPE_TARGET}
	SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/deps/magnum-plugins"
	LIST_SEPARATOR "#"
	CMAKE_ARGS
		"-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
		"-DWITH_FREETYPEFONT=ON"
		${CUSTOM_MAGNUMPLUGINS_ARGS}
	TEST_EXCLUDE_FROM_MAIN 1
)
ExternalProject_Add(simple-platformer
	DEPENDS
		Corrade
		Magnum
		MagnumPlugins
	SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/src"
	LIST_SEPARATOR "#"
	CMAKE_ARGS
		"-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
		"-DCMAKE_MODULE_PATH=${CMAKE_INSTALL_PREFIX}#${CMAKE_CURRENT_SOURCE_DIR}/deps/magnum/modules"
		${CUSTOM_SIMPLEPLATFORMER_ARGS}
)
